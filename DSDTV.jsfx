desc:DSDTV

in_pin:left input
in_pin:right input
out_pin:left output
out_pin:right output

slider1:delta=0.82<0.7,0.9,0.001>delta

@init
marker = 0x050000;
mult = 2^23;
invmult = 2^-23;
toggle = 1;
commonSrate = 176400*16;
desiredSrate = 500000;
invdesired = 1/desiredSrate;
upsampling = commonSrate / srate; // ensure that this value is integer 
downsampling = commonSrate / desiredSrate;
invdown = desiredSrate / commonSrate;
invup = srate / commonSrate;
sixpi = 2*$pi;

omdelta = 1-delta;

position = 0;

out = -1;
pimult = $pi*2;
inv305 = 1/305;

invhpos = 1 / (32-3);

inv32 = 1/32;

togglefreq = 2*$pi/srate*0.5;


function display() (
  toggle * 0.01 * cos(pimult*sqrt((hpos*hpos + vpos*vpos)));
  //0;
);

function upsampled_code()
(
  position = (position + 1) % 8820000000; // commonsrate  * invdown
  prev = fractional;
  fractional = position * invdown;
  wrapped = fractional*invdesired;
  wrapped = wrapped-(0|wrapped);
  (fractional|0) - (prev|0) ? (
    int = (int+1) % 20000; // 312*32
    hcount = 0|(int * inv32);
    hblank = int % 32;
    hblank == 0 
      || (hblank == 1 && 4 <= hcount && hcount < 309)
      || (hblank == 1 && 317 <= hcount && hcount < 622)
      || ((hcount < 315 && (hcount < 2 || hcount > 312) ) && hblank != 15 && hblank != 31)
      || (hcount == 2 && hblank < 15)
      || (hcount == 312 && hblank > 15 && hblank != 31) ? out_unf = -1 : out_unf = 0;
  );
  5 <= hcount && hcount < 310 && (3 <= hblank && hblank < 31) ? (
    hpos = (wrapped + (hblank - 3)) * invhpos - 0.5;
    vpos = (hcount - 5.5) * inv305 - 0.5;
    out_unf = 0.4 + display();
  );
  317 <= hcount && hcount < 622 && (3 <= hblank && hblank < 31) ? (
    hpos = (wrapped + (hblank - 3)) * invhpos - 0.5;
    vpos = (hcount - 316.5) * inv305 - 0.5;
    out_unf = 0.4 + display();
  );
  avg += out_unf;
  out_unf_1 = (1+out_unf)*0.5;
  v1 = out_unf_1 - ret;
  out = out * delta + v1 * omdelta;
  ret = out > 0.5
)

@sample

total = 0;
avg = 0;
loop(upsampling,
  upsampled_code();
  total *= 2;
  total += ret;
);

marker != 0x050000 ? marker = 0x050000 : marker = 0xFA0000;

total = total | marker;

total&0x800000 ? total = -1-(total~0xffffff);

spl0 = spl1 = total * invmult;
c += 1;
